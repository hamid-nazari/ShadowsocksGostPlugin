apply plugin: 'com.android.application'

android {
    compileSdkVersion 33
    namespace "com.github.shadowsocks.plugin.gost"
    defaultConfig {
        applicationId "${android.namespace}"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 400001
        versionName "4.0.0-rc1"
    }
    signingConfigs {
        releaseConfig {
            keyAlias "default"
            keyPassword System.getenv("KEY_PASS")
            storePassword System.getenv("KEY_PASS")
            storeFile file(System.getenv("HOME") + "/.android.jks")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.releaseConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    android.ndkVersion "26.2.11394342"
}

dependencies {
    implementation 'com.github.shadowsocks:plugin:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
}

task buildGoLibrary(type: Exec) {
    environment ([ANDROID_NDK_ROOT: android.ndkDirectory])
    commandLine ('../build.sh')
    inputs.files '../build.sh'
    outputs.dir 'src/main/jniLibs'
}

task cleanGoLibrary(type: Delete) {
    delete "src/main/jniLibs"
}

cleanGoLibrary.shouldRunAfter clean

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("preDebugBuild") || theTask.name.equals("preReleaseBuild")) {
        theTask.dependsOn "buildGoLibrary"
    }
}


